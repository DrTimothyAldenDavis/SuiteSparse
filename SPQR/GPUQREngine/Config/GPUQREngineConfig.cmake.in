#-------------------------------------------------------------------------------
# SuiteSparse/GPUQREngine/cmake_modules/GPUQREngineConfig.cmake
#-------------------------------------------------------------------------------

# The following copyright and license applies to just this file only, not to
# the library itself:
# GPUQREngineConfig.cmake, Copyright (c) 2023, Timothy A. Davis.  All Rights Reserved.
# SPDX-License-Identifier: BSD-3-clause

#-------------------------------------------------------------------------------

# Finds the GPUQREngine include file and compiled library.
# The following targets are defined:
#   SuiteSparse::GPUQREngine           - for the shared library (if available)
#   SuiteSparse::GPUQREngine_static    - for the static library (if available)

# For backward compatibility the following variables are set:

# GPUQRENGINE_INCLUDE_DIR - where to find GPUQREngine.hpp
# GPUQRENGINE_LIBRARY     - dynamic GPUQREngine library
# GPUQRENGINE_STATIC      - static GPUQREngine library
# GPUQRENGINE_LIBRARIES   - libraries when using GPUQREngine
# GPUQRENGINE_FOUND       - true if GPUQREngine found

# Set ``CMAKE_MODULE_PATH`` to the parent folder where this module file is
# installed.

#-------------------------------------------------------------------------------

@PACKAGE_INIT@

set ( GPUQRENGINE_DATE "@GPUQRENGINE_DATE@" )
set ( GPUQRENGINE_VERSION_MAJOR @GPUQRENGINE_VERSION_MAJOR@ )
set ( GPUQRENGINE_VERSION_MINOR @GPUQRENGINE_VERSION_MINOR@ )
set ( GPUQRENGINE_VERSION_PATCH @GPUQRENGINE_VERSION_SUB@ )
set ( GPUQRENGINE_VERSION "@GPUQRENGINE_VERSION_MAJOR@.@GPUQRENGINE_VERSION_MINOR@.@GPUQRENGINE_VERSION_SUB@" )

include ( ${CMAKE_CURRENT_LIST_DIR}/GPUQREngineTargets.cmake )

# The following is only for backward compatibility with FindGPUQREngine.

set ( _target_shared SuiteSparse::GPUQREngine )
set ( _target_static SuiteSparse::GPUQREngine_static )
set ( _var_prefix "GPUQRENGINE" )

get_target_property ( ${_var_prefix}_INCLUDE_DIR ${_target_shared} INTERFACE_INCLUDE_DIRECTORIES )
if ( ${_var_prefix}_INCLUDE_DIR )
    # First item in SuiteSparse targets contains the "main" header directory.
    list ( GET ${_var_prefix}_INCLUDE_DIR 0 ${_var_prefix}_INCLUDE_DIR )
endif ( )
get_target_property ( ${_var_prefix}_LIBRARY ${_target_shared} IMPORTED_IMPLIB )
if ( NOT ${_var_prefix}_LIBRARY )
    get_target_property ( _library_chk ${_target_shared} IMPORTED_LOCATION )
    if ( EXISTS ${_library_chk} )
        set ( ${_var_prefix}_LIBRARY ${_library_chk} )
    endif ( )
endif ( )
if ( TARGET ${_target_static} )
    get_target_property ( ${_var_prefix}_STATIC ${_target_static} IMPORTED_LOCATION )
endif ( )

# Check for most common build types
set ( _config_types "Debug" "Release" "RelWithDebInfo" "MinSizeRel" "None" )

get_property ( _isMultiConfig GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG )
if ( _isMultiConfig )
    # For multi-configuration generators (e.g., Visual Studio), prefer those
    # configurations.
    list ( PREPEND _config_types ${CMAKE_CONFIGURATION_TYPES} )
else ( )
    # For single-configuration generators, prefer the current configuration.
    list ( PREPEND _config_types ${CMAKE_BUILD_TYPE} )
endif ( )

list ( REMOVE_DUPLICATES _config_types )

foreach ( _config ${_config_types} )
    string ( TOUPPER ${_config} _uc_config )
    if ( NOT ${_var_prefix}_LIBRARY )
        get_target_property ( _library_chk ${_target_shared}
            IMPORTED_IMPLIB_${_uc_config} )
        if ( EXISTS ${_library_chk} )
            set ( ${_var_prefix}_LIBRARY ${_library_chk} )
        endif ( )
    endif ( )
    if ( NOT ${_var_prefix}_LIBRARY )
        get_target_property ( _library_chk ${_target_shared}
            IMPORTED_LOCATION_${_uc_config} )
        if ( EXISTS ${_library_chk} )
            set ( ${_var_prefix}_LIBRARY ${_library_chk} )
        endif ( )
    endif ( )
    if ( TARGET ${_target_static} AND NOT ${_var_prefix}_STATIC )
        get_target_property ( _library_chk ${_target_static}
            IMPORTED_LOCATION_${_uc_config} )
        if ( EXISTS ${_library_chk} )
            set ( ${_var_prefix}_STATIC ${_library_chk} )
        endif ( )
    endif ( )
endforeach ( )

set ( GPUQRENGINE_LIBRARIES ${GPUQRENGINE_LIBRARY} )

macro ( suitesparse_check_exist _var _files )
  # ignore generator expressions
  string ( GENEX_STRIP "${_files}" _files2 )

  foreach ( _file ${_files2} )
    if ( NOT EXISTS "${_file}" )
      message ( FATAL_ERROR "File or directory ${_file} referenced by variable ${_var} does not exist!" )
    endif ( )
  endforeach ()
endmacro ( )

suitesparse_check_exist ( GPUQRENGINE_INCLUDE_DIR ${GPUQRENGINE_INCLUDE_DIR} )
suitesparse_check_exist ( GPUQRENGINE_LIBRARY ${GPUQRENGINE_LIBRARY} )

message ( STATUS "GPUQREngine version: ${GPUQRENGINE_VERSION}" )
message ( STATUS "GPUQREngine include: ${GPUQRENGINE_INCLUDE_DIR}" )
message ( STATUS "GPUQREngine library: ${GPUQRENGINE_LIBRARY}" )
message ( STATUS "GPUQREngine static:  ${GPUQRENGINE_STATIC}" )
